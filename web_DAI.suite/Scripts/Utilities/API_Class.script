on initialize
	
end initialize

on GrabModelFromDAI DAIServer, Username ,Password
	put DAIServer into My DAIServer
	put Username into my Username
	put Password into my Password
	
	me.GetAccessToken
	
	me.GetGroups
	me.ChooseGroup
	
	me.GetModels
	me.ChooseModel
	
end GrabModelFromDAI


on GetAccessToken
	put My DAIServer & "/ai/auth" into URL
	put my Username into RB.username
	put my Password into RB.password
	
	log URL
	
	DAI_API(httpMethod:"POST",url:URL,requestBody:RB)
	put JSONValue(api().responsebody) into responsebody
	put responsebody."access_token" into my access_token
	
	if api().statusCode = "200" then
		Log "Able to Access DAI Server"
	else
		logerror "Unable to Access DAI Server (Bad Username,Password, or DAI Server)"
		logwarning responsebody
		logwarning API()
		exit all
	end if
end GetAccessToken


on GetGroups
	put My DAIServer & "/ai/groups" into URL
	put "bearer" && my access_token into RH."Authorization"
	put "application/x-www-form-urlencoded" into RH."Content-Type"
	DAI_API(httpMethod:"GET",url:URL,requestheaders:RH)
	put JSONValue(api().responseBody) into GroupList
	put GroupList into my GroupList
end GetGroups


to ChooseGroup
	put my GroupList into GroupList
	put the number of items of GroupList into GroupCount
	repeat GroupCount times
		put item repeatindex() of GroupList into GroupName
		insert GroupName.groupname into PickGroupList.(GroupName.id)
		insert GroupName.groupname  into DisplayGroupPick
	end repeat
	
	answer "Your Group to Filter though the Models"  from list DisplayGroupPick with "Ok" or "Cancel" title "Please Choose"
	put it into PickedGroup
	put empty into it	
	if PickedGroup = "Cancel" or if PickedGroup is empty then
		logwarning "User did not pick a Group"
		exit all
	end if
	
	log "Group Picked:"&& PickedGroup
	put PickedGroup into my PickedGroup
	return PickedGroup
end ChooseGroup



on GetModels
	log "Grabbing information about Your Models"
	
	put My DAIServer & "/ai/models" into URL
	put "bearer" && my access_token into RH."Authorization"
	put "application/x-www-form-urlencoded" into RH."Content-Type"
	DAI_API(httpMethod:"GET",url:URL,requestheaders:RH)
	put JSONValue(api().responseBody) into ModelList
	
	put the number of items of ModelList into ModelCount
	//-------------
	if my PickedGroup  is empty
		put my Username into PickedGroup
	else
		put my PickedGroup into PickedGroup
		
	end if
	
	//-----------
	repeat ModelCount Times
		put item repeatindex() of ModelList into Modelinfo
		if Modelinfo.group.name = PickedGroup then
			insert (name:Modelinfo.name,ID:Modelinfo.modeluuid) into ListofModels
		end if
	end repeat
	
	if  ListofModels is not a list then
		LogError "No Models in the Group"
		Exit all
	end if
	
	put ListofModels into my ListofModels
end GetModels



on ChooseModel
	put me.ListofModels into UserModels
	
	put the number of items of UserModels into ModelCount
	repeat ModelCount times
		put item repeatindex() of UserModels into InfoOnModel
		insert (InfoOnModel.name) into ModelChooseList
		put InfoOnModel.id into ModelListID.(InfoOnModel.name)
		
	end repeat
	answer "Your Model to Build a Suite From"  from list ModelChooseList with "Ok" or "Cancel" title "Please Choose"
	put it into PickedModel
	put empty into it	
	if PickedModel = "Cancel" or if PickedModel is empty then
		logwarning "User did not pick a Model"
		exit all
	end if
	
	put ModelListID.(PickedModel) into my ModelUID
end ChooseModel

//--------------------------------------------
function GetModelJSON 
	put my ModelUID into Model_ID
	put My DAIServer & "/ai/models/"&Model_ID into URL
	put "bearer" && my access_token into RH."Authorization"
	put "application/x-www-form-urlencoded" into RH."Content-Type"
	DAI_API(httpMethod:"GET",url:URL,requestheaders:RH)
	put JSONValue(api().responseBody) into Model
	
	return Model
end GetModelJSON

function GetRuns
	put my ModelUID into Model_ID
	put my DAIServer & "/ai/models/"&Model_ID&"/runs" into URL
	put "bearer" && my access_token into RH."Authorization"
	put "application/x-www-form-urlencoded" into RH."Content-Type"
	DAI_API(httpMethod:"GET",url:URL,requestheaders:RH)
	put JSONValue(api().responseBody) into RunLogs
	
	return RunLogs
end GetRuns


on UpdateModel  DAIModel
	
	put DAIModel.modeluuid into Model_ID
	put DAIModel.name into RBName
	put  DAIModel.group.name into RBGroup
	put JSONFormat(DAIModel.modeljson) into DAIMMJ
	
	//-------------------------------------
	put My DAIServer & "/ai/models/"&Model_ID into URL
	put "bearer" && my access_token into RH."Authorization"
	put "application/x-www-form-urlencoded" into RH."Content-Type"
	DAI_API(httpMethod:"PUT",url:URL,requestheaders:RH,requestbody:("group":RBGroup,"name":RBName,"modeljson":DAIMMJ))
	put JSONValue(api().responseBody) into responsebody
	if api().statusCode = "200" then
		Log "Model Updated:" && RBName
	else
		logerror "Could not update Model:" && RBName
		logwarning responsebody
		logwarning API()
		put JSONFormat (API()) into file (ResourcePath() &"/requestbody.json")
	end if
	
	
end UpdateModel



on CreateModel DAIModel, NewName
	//put DAIModel.name into RBName
	put NewName into RBName
	put  DAIModel.group.name into RBGroup
	put JSONFormat(DAIModel.modeljson) into DAIMMJ
	
	put My DAIServer & "/ai/models" into URL
	put "bearer" && my access_token into RH."Authorization"
	put "application/x-www-form-urlencoded" into RH."Content-Type"
	DAI_API(httpMethod:"POST",url:URL,requestheaders:RH,requestbody:(group:RBGroup,"name":RBName,"modeljson":DAIMMJ))
	
	
	put JSONValue(api().responseBody) into responsebody
	if api().statusCode = "200" then
		Log "Model Created:" && RBName
	else
		logerror "Could not update Model:" && RBName
		logwarning responsebody
		logwarning API()
		put JSONFormat (API()) into file (ResourcePath() &"/requestbody.json")
	end if
	
	
end CreateModel



