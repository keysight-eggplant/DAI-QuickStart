
on BuildSuite ( SuitePath,AIJson )
	put AIJson into AIJsonPath
	split AIJsonPath by "/"
	put  item -1 of AIJsonPath into simplename
	delete ".json" in simplename
	
	//-----Switches 
	put False into my ShowFramework // Show I show the Customer Framework?
	put False into my WriteLogsSnippets //Should I Write Extra Log Statement to the Snippets?
	put False into my DeleteOriginalJson //Should I Delete the Original Json?
	//-----
	
	
	me.CheckInformation SuitePath,simplename, AIJson
	me.BuildAIFrameWork AIJson 
	me.BuildNewAIJson  simplename
	me.CreateNewSuite SuitePath,simplename
	me.createScripts 
	
	if my DeleteOriginalJson is True then
		Logwarning "Deleting Original Json:"&& AIJson
		delete file AIJson
	end if
	
end BuildSuite

on CheckInformation BasePathSuite , simplename , ResoucePather
	if folder BasePathSuite exists then
	else
		logerror "Unable to Fine Folder:" && BasePathSuite
		Exit all	
	end if
	
	if file ResoucePather  exists then
	else
		logerror "Unable to Fine Model Json:" && ResoucePather
		Exit all	
	end if
	
	put BasePathSuite &"/" &simplename &".suite" into NewPathSuite
	if folder NewPathSuite  exists then
		logwarning "Deleting Existing Suite:"&& NewPathSuite
		try
			delete folder NewPathSuite
		catch
			logerror "Unable to Deleting Existing Suite:"&& NewPathSuite
		end try
	end if
end CheckInformation

to BuildAIFrameWork ResoucePather
	//----------------------------Adding to Framework
	put 0 into ListOfState."GlobalActions"
	put "GlobalActions" into IDMapState."0"
	//----------------------------
	put JSONValue(file (ResoucePather)) into AIModel
	put AIModel.elements into AIE
	put the number of items of AIE into Max
	replace space with "_" in AIE
	
	
	repeat max times
		put item repeatindex() of AIE into SingleAIE
		if SingleAIE.type = "Action" then
			put SingleAIE.name into ActionName
			insert   ActionName into ListOfAction.(SingleAIE."parentid")
		else if  SingleAIE.type = "State" then
			put SingleAIE.name into StateName
			put  SingleAIE."id" into ListOfState.(StateName)
			put StateName into IDMapState.(SingleAIE."id") //--ID Mapping
		end if
		
		if SingleAIE.stateType = "Start"
			insert SingleAIE.name into ListofStartStates
		end if
		
	end repeat
	//------------------------------------------------------------------------------
	//-----------------------------------------------------------------------------------------
	put keys(ListOfState) into StateKeys
	put the number of items of StateKeys into StateCount
	repeat StateCount times
		put item repeatindex() of StateKeys into SSK //--Single State Key
		put ListOfState.(SSK) into TheID
		put ListOfAction.(TheID) into ActionsinState
		put ActionsinState into  FrameworkData.(SSK)
	end repeat
	
	//-----------Modify FrameworkData
	delete FrameworkData.Cleanup
	delete FrameworkData.Error
	delete FrameworkData.Exit
	
	
	put keys(FrameworkData) into FDKeys
	put the number of items of FDKeys into FDKeyCount
	repeat FDKeyCount Times
		put item repeatindex() of FDKeys into SKD //--Single Key Data
		if SKD not equal "GlobalActions"  then
			//if    ListofStartStates  contains SKD  then //-Removed on 2/17/2021
			//else
			if FrameworkData.(SKD) is a  list
				insert "StateCall" before first item of  FrameworkData.(SKD) //--Add StateCall
			else	
				put "StateCall" before first item of  FrameworkData.(SKD) //--Add StateCall
			end if
			//end if
		end if	
	end repeat
	
	
	put ["Cleanup","ExitModel","ErrorModel"]  into FrameworkData.GlobalStates
	//-----------Modify FrameworkData
	
	
	put AIModel into My AIModel
	put  IDMapState into my IDMapState
	put FrameworkData  into my FrameworkData
	
	
	if my ShowFramework = True then
		log "FrameworkData:" & FrameworkData
		log "IDMapState:" & IDMapState
		PauseScript
	end if
	
end BuildAIFrameWork





on BuildNewAIJson simplename
	put my IDMapState into IDMapState
	put my AIModel into AIModel
	//-----------------------------------------------
	put AIModel.elements into AIE
	put the number of items of AIE into Max
	repeat max times
		put item repeatindex() of AIE into SingleAIE
		//********************************************
		put SingleAIE.name into SingleName
		replace space with "_" in SingleName
		//----For TroubleShooting
		//log SingleAIE
		//---
		if SingleAIE.type = "Action" then //---Do something to Actions
			put SingleAIE.parentid into IDS
			if IDS is not empty then
				put IDMapState.(IDS) into StateName
			end if
			
			put StateName &".script:"& SingleName into AIScriptPath
			insert [{fullName:SingleName, name:SingleName, scriptPath:"Snippets/"&AIScriptPath, type:"normal"}]  into SingleAIE."snippets"
			
		else if  SingleAIE.type = "State" then //---Do something to States
			put SingleName &".script:"& "StateCall" into AIScriptPath
			
			if SingleAIE.stateType = "Start"
				insert [{fullName:"ConfigFile", name:"ConfigFile", scriptPath:"SetupEnv/StartupEnv.script", type:"normal"}]  into SingleAIE."snippets"
				insert [{fullName:SingleName, name:SingleName, scriptPath:"Snippets/"&AIScriptPath, type:"normal"}]  into SingleAIE."snippets"
			else if SingleAIE.stateType = "Exception"
				insert [{fullName:"ConfigFile", name:"ConfigFile", scriptPath:"Snippets/GlobalStates.script:ErrorModel", type:"normal"}]  into SingleAIE."snippets"
			else if SingleAIE.stateType = "Cleanup"
				insert [{fullName:"ConfigFile", name:"ConfigFile", scriptPath:"Snippets/GlobalStates.script:Cleanup", type:"normal"}]  into SingleAIE."snippets"
			else if SingleAIE.name = "Exit"
				insert [{fullName:"ConfigFile", name:"ConfigFile", scriptPath:"Snippets/GlobalStates.script:ExitModel", type:"normal"}]  into SingleAIE."snippets"
			else 
				insert [{fullName:SingleName, name:SingleName, scriptPath:"Snippets/"&AIScriptPath, type:"normal"}]  into SingleAIE."snippets"
			end if
			
		end if
		//********************************************
		insert SingleAIE into NEWAIModel.elements 
	end repeat
	
	delete AIModel.elements
	delete  AIModel.suitePath
	put NEWAIModel.elements into AIModel.elements
	put simplename &".suite" into AIModel.suitePath
	
	
	put AIModel into my AIModel
	
end BuildNewAIJson



on CreateNewSuite BasePathSuite , simplename
	put BasePathSuite &"/" &simplename &".suite" into NewPathSuite
	
	copy folder ResourcePath() &"/TemplateSuite.suite" to BasePathSuite
	rename folder BasePathSuite&"/TemplateSuite.suite" to simplename &".suite"
	wait 2
	Log "Created New Suite:"&& NewPathSuite
	//-------------
	put my AIModel into AIModel
	put JSONFormat (AIModel) into file ResourcePath (simplename& ".JSON")
	move file ResourcePath (simplename&".json") to NewPathSuite & "/Resources/"
	//-------------
	
	//--------
	put NewPathSuite  into my NewPathSuite
end CreateNewSuite



on createScripts 
	put my FrameworkData  into Framework
	put my NewPathSuite into SuitePath
	//-----------------
	put keys(Framework) into  ScriptList
	put the number  of items of ScriptList into  ScriptCount
	repeat ScriptCount times
		put item  repeatindex() of ScriptList into ScriptName
		put Framework.(ScriptName) into  HandlersNames
		put SuitePath &"/Scripts/Snippets/" & ScriptName &".script" into ScriptPath
		//create folder SuitePath &"/Images/" & ScriptName //--Created Image Folders
		create file ScriptPath
		open  file ScriptPath
		//------
		put the number of  items  of HandlersNames into HandlerCount
		repeat HandlerCount times
			put   item repeatindex() of HandlersNames into SHN
			write "on" && SHN to file ScriptPath 
			write return to file ScriptPath
			write "//Workflow for" && SHN to file ScriptPath
			----------------------------------------
			if my WriteLogsSnippets = True then
				write return to file ScriptPath
				write <<log "Called from (>>&ScriptName&<<:>>&SHN&<<)">> to file ScriptPath
				write return to file ScriptPath
				write <<wait 4>> to file ScriptPath
				write return to file ScriptPath
			end if
			----------------------------------------
			write return to file ScriptPath 
			write return to file ScriptPath 
			write return to file ScriptPath 
			write "end" && SHN to file ScriptPath 
			write return to file ScriptPath 
			write return to file ScriptPath
			//log ScriptName && SHN
		end repeat
		close file ScriptPath
	end repeat
end createScripts


