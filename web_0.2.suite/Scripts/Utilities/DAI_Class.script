function GrabModelFromDAI
	me.GetAccessToken
	me.GetModels
	me.ChooseModel
	
	put me.GetModelJSON into AiFullModel
	return AiFullModel
end GrabModelFromDAI

on GetAccessToken
	put My DAIServer & "/ai/auth" into URL
	put my Username into RB.username
	put my Password into RB.password
	
	DAI_API(httpMethod:"POST",url:URL,requestBody:RB)
	put JSONValue(api().responsebody) into responsebody
	put responsebody."access_token" into my access_token
	
	
end GetAccessToken


function GetGroups

	put My DAIServer & "/ai/groups" into URL
	put "bearer" && my access_token into RH."Authorization"
	put "application/x-www-form-urlencoded" into RH."Content-Type"
	DAI_API(httpMethod:"GET",url:URL,requestheaders:RH)
	put JSONValue(api().responseBody) into GroupList
	
	put the number of items of GroupList into GroupCount
	repeat GroupCount times
		put item repeatindex() of GroupList into GroupName
		insert GroupName.groupname into PickGroupList.(GroupName.id)
		insert GroupName.groupname  into DisplayGroupPick
	end repeat
	
	
	
	answer "Your Group to Filter though the Models"  from list DisplayGroupPick with "Ok" or "Cancel" title "Please Choose"
	put it into PickedGroup
	put empty into it	
	if PickedGroup = "Cancel" or if PickedGroup is empty then
		logwarning "User did not pick a Group"
		exit all
	end if
	return PickedGroup
end GetGroups




on GetModels
	log "Grabbing information about Your Models"
	
	put My DAIServer & "/ai/models" into URL
	put "bearer" && my access_token into RH."Authorization"
	put "application/x-www-form-urlencoded" into RH."Content-Type"
	DAI_API(httpMethod:"GET",url:URL,requestheaders:RH)
	put JSONValue(api().responseBody) into ModelList
	
	put the number of items of ModelList into ModelCount
	put me.GetGroups into PickedGroup
	
	//-----------
	repeat ModelCount Times
		put item repeatindex() of ModelList into Modelinfo
		if Modelinfo.group.name = PickedGroup then
			insert (name:Modelinfo.name,ID:Modelinfo.modeluuid) into UserModels
		end if
	end repeat
	
	put UserModels into my ListofModels
end GetModels



on ChooseModel
	put me.ListofModels into UserModels
	
	put the number of items of UserModels into ModelCount
	repeat ModelCount times
		put item repeatindex() of UserModels into InfoOnModel
		insert (InfoOnModel.name) into ModelChooseList
		put InfoOnModel.id into ModelListID.(InfoOnModel.name)
		
	end repeat
	answer "Your Model to Build a Suite From"  from list ModelChooseList with "Ok" or "Cancel" title "Please Choose"
	put it into PickedModel
	put empty into it	
	if PickedModel = "Cancel" or if PickedModel is empty then
		logwarning "User did not pick a Model"
		exit all
	end if
	
	put ModelListID.(PickedModel) into my ModelUID
	put PickedModel into my PickedModelName
end ChooseModel

//--------------------------------------------
function GetModelJSON 
	put my ModelUID into Model_ID
	put My DAIServer & "/ai/models/"&Model_ID into URL
	put "bearer" && my access_token into RH."Authorization"
	put "application/x-www-form-urlencoded" into RH."Content-Type"
	DAI_API(httpMethod:"GET",url:URL,requestheaders:RH)
	put JSONValue(api().responseBody) into Model
	
	put Model into my FullAiModel
	return Model
end GetModelJSON



on UpdateModel  PreRBMJ
	put my ModelUID into Model_ID
	put my PickedModelName into RBName
	put my FullAiModel.group.name into RBGroup
	put JSONFormat(PreRBMJ) into RBMJ
	
	
	put My DAIServer & "/ai/models/"&Model_ID into URL
	put "bearer" && my access_token into RH."Authorization"
	put "application/x-www-form-urlencoded" into RH."Content-Type"
	DAI_API(httpMethod:"PUT",url:URL,requestheaders:RH,requestbody:("group":RBGroup,"name":RBName,"modeljson":RBMJ))
	put JSONValue(api().responseBody) into responsebody
	if api().statusCode = "200" then
		Log "Model Updated:" && RBName
	else
		logerror "Model Could not Update"
		logwarning responsebody
		logwarning API()
	end if
	
end UpdateModel



