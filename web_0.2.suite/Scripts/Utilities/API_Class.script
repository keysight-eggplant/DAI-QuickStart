on initialize
	
end initialize

on GrabModelFromDAI infoDAI
	put item 1 of infoDAI into My DAIServer
	put item 2 of infoDAI into my Username
	put item 3 of infoDAI into my Password
	
	me.GetAccessToken
	
	me.GetGroup
	me.ChooseGroup
	
	me.GetModel
	me.ChooseModel
	
end GrabModelFromDAI

on GetAccessToken
	put My DAIServer & "/ai/auth" into URL
	put my Username into RB.username
	put my Password into RB.password
	
	DAI_API(httpMethod:"POST",url:URL,requestBody:RB)
	put JSONValue(api().responsebody) into responsebody
	put responsebody."access_token" into my access_token
	
	if api().statusCode = "200" then
		Log "Able to Access DAI Server"
	else
		logerror "Unable to Access DAI Server (Bad Username,Password, or DAI Server)"
		logwarning responsebody
		logwarning API()
		exit all
	end if
end GetAccessToken


on GetGroup
	put My DAIServer & "/ai/groups" into URL
	put "bearer" && my access_token into RH."Authorization"
	put "application/x-www-form-urlencoded" into RH."Content-Type"
	DAI_API(httpMethod:"GET",url:URL,requestheaders:RH)
	put JSONValue(api().responseBody) into GroupList
	put GroupList into my GroupList
end GetGroup


to ChooseGroup
	put my GroupList into GroupList
	put the number of items of GroupList into GroupCount
	repeat GroupCount times
		put item repeatindex() of GroupList into GroupName
		insert GroupName.groupname into PickGroupList.(GroupName.id)
		insert GroupName.groupname  into DisplayGroupPick
	end repeat
	
	answer "Your Group to Filter though the Models"  from list DisplayGroupPick with "Ok" or "Cancel" title "Please Choose"
	put it into PickedGroup
	put empty into it	
	if PickedGroup = "Cancel" or if PickedGroup is empty then
		logwarning "User did not pick a Group"
		exit all
	end if
	
	log "Group Picked:"&& PickedGroup
	put PickedGroup into my PickedGroup
	return PickedGroup
end ChooseGroup


on GetModel
	log "Grabbing information about Your Models"
	
	put My DAIServer & "/ai/models" into URL
	put "bearer" && my access_token into RH."Authorization"
	put "application/x-www-form-urlencoded" into RH."Content-Type"
	DAI_API(httpMethod:"GET",url:URL,requestheaders:RH)
	put JSONValue(api().responseBody) into ModelList
	
	put the number of items of ModelList into ModelCount
	//-------------
	put my PickedGroup into PickedGroup
	
	//-----------
	repeat ModelCount Times
		put item repeatindex() of ModelList into Modelinfo
		if Modelinfo.group.name = PickedGroup then
			insert (name:Modelinfo.name,ID:Modelinfo.modeluuid) into UserModels
		end if
	end repeat
	
	if  UserModels is not a list then
		LogError "No Models in the Group"
		Exit all
	end if
	
	put UserModels into my ListofModels
end GetModel



on ChooseModel
	put me.ListofModels into UserModels
	
	put the number of items of UserModels into ModelCount
	repeat ModelCount times
		put item repeatindex() of UserModels into InfoOnModel
		insert (InfoOnModel.name) into ModelChooseList
		put InfoOnModel.id into ModelListID.(InfoOnModel.name)
		
	end repeat
	answer "Your Model to Build a Suite From"  from list ModelChooseList with "Ok" or "Cancel" title "Please Choose"
	put it into PickedModel
	put empty into it	
	if PickedModel = "Cancel" or if PickedModel is empty then
		logwarning "User did not pick a Model"
		exit all
	end if
	
	put ModelListID.(PickedModel) into my ModelUID
	put PickedModel into my PickedModelName
end ChooseModel

//--------------------------------------------
function GetModelJSON 
	put my ModelUID into Model_ID
	put My DAIServer & "/ai/models/"&Model_ID into URL
	put "bearer" && my access_token into RH."Authorization"
	put "application/x-www-form-urlencoded" into RH."Content-Type"
	DAI_API(httpMethod:"GET",url:URL,requestheaders:RH)
	put JSONValue(api().responseBody) into Model
	
	return Model
end GetModelJSON



on UpdateModel  DAIModel
	put my ModelUID into Model_ID
	put my PickedModelName into RBName
	put my PickedGroup into RBGroup
	put JSONFormat(DAIModel.modeljson) into RBJFMJ
	
	//-------------------------------------
	put My DAIServer & "/ai/models/"&Model_ID into URL
	put "bearer" && my access_token into RH."Authorization"
	put "application/x-www-form-urlencoded" into RH."Content-Type"
	DAI_API(httpMethod:"PUT",url:URL,requestheaders:RH,requestbody:("group":RBGroup,"name":RBName,"modeljson":RBJFMJ))
	put JSONValue(api().responseBody) into responsebody
	if api().statusCode = "200" then
		Log "Model Updated:" && RBName
	else
		logerror "Could not update Model:" && RBName
		logwarning responsebody
		logwarning API()
		
	end if
	
	//put JSONFormat (API()) into file (ResourcePath() &"/requestbody.json")
end UpdateModel



