
to handle WaitForItem MyItem
	Set the ScriptLogging to Silent
	set the searchrectangle to "WebFramework/ScreenPart".ScreenPart (Universal SearchArea)
	try 
		MoveTo {image:MyItem,waitfor:20}
	catch 
		MoveTo {text:MyItem,waitfor:20}
	end try
	Run "WebFramework/ResetVariables"
	Set the ScriptLogging to On 
end WaitForItem

to handle MouseOver ScreenObject //used for hovering over elements to bring up sub menus or transient elements
	// if you need to isolate a specific area then set the searchrectangle (global property) in your snippet of code  
	Set the ScriptLogging to Silent
	try 
		MoveTo {image:ScreenObject,waitfor:20}
	catch 
		MoveTo {text:ScreenObject,waitfor:20}
	end try
	Run "WebFramework/ResetVariables"
	Set the ScriptLogging to On 
end MouseOver

to handle ClickEvent ScreenObject,ExpOutcome
	//to handle all click event type actions navigation as well as standalone click events....
	Set the ScriptLogging to Silent
	try 
		click image:ScreenObject
	catch 
		click text:ScreenObject
	end try 
	wait 0.5 // to handle state transition better...
	if ExpOutcome is not empty 
		set the searchrectangle to "WebFramework/ScreenPart".ScreenPart (Universal SearchArea)
		try 					
			waitfor 20, image:ExpOutcome
		catch 
			waitfor 20, text:ExpOutcome
		end try 	
	end if
	Run "WebFramework/ResetVariables"
	Set the ScriptLogging to On
end ClickEvent

to handle TextEntry MyItem
	typetext controlKey,"a"
	typetext MyItem
	Click Universal Unfocus
end TextEntry

to handle ScrollingDown ScreenObject
	repeat until imagefound(0,(Universal DataType):ScreenObject,searchrectangle:"WebFramework/ScreenPart".ScreenPart(Universal SearchArea))
		Typetext downArrow repeated Universal LoopCounter times -- the loopCounter variable can be configured in the configfile.script 
		log repeatindex()
		if repeatindex() > Universal LoopCounter then 
			typetext homeKey
			exit repeat
		end if
		wait 0.5
	end repeat
	Run "WebFramework/ResetVariables"
end ScrollingDown

to handle ScrollingUp ScreenObject
	repeat until imagefound(0,(Universal DataType):ScreenObject,searchrectangle:"WebFramework/ScreenPart".ScreenPart(Universal SearchArea))
		Typetext upArrow repeated Universal LoopCounter times -- the loopCounter variable can be configured in the configfile.script 
		if repeatindex() > Universal LoopCounter then 
			typetext endKey
			exit repeat
		end if
		wait 0.5
	end repeat
	Run "WebFramework/ResetVariables"
end ScrollingUp


